load("@bazel_gazelle//:def.bzl", "gazelle")
load("@io_bazel_rules_docker//container:container.bzl", "container_image", "container_bundle")

gazelle(
    name = "gazelle",
    external = "vendored",
    extra_args = [
        "-build_file_name",
        "BUILD,BUILD.bazel",
    ],
    prefix = "github.com/jetstack/navigator",
)

DOCKERIZED_BINARIES = {
    "controller": {
        "base": "@alpine//image",
        "target": "//cmd/controller:controller",
    },
    "apiserver": {
        "base": "@alpine//image",
        "target": "//cmd/apiserver:apiserver",
    },
}

DOCKERIZED_PILOTS = {
    "pilot-elasticsearch": {
        "base": "@alpine//image",
        "target": "//cmd/pilot-elasticsearch:pilot-elasticsearch",
    },
    "pilot-cassandra": {
        "base": "//:cassandra-base",
        "target": "//cmd/pilot-cassandra:pilot-cassandra",
    },
}

container_image(
    name = "cassandra-base",
    base = "@alpine//image",
    files = [
        "//build/pilot-cassandra:image-deps",
    ],
)

[container_image(
    name = binary + "-internal",
    base = meta["base"],
    cmd = ["/" + binary],
    files = [meta["target"]],
    symlinks = {
        "/usr/bin/navigator-" + binary: "/" + binary,
    },
) for binary, meta in DOCKERIZED_BINARIES.items()]

[container_bundle(
    name = binary,
    images = {
        "jetstackexperimental/navigator-%s:{STABLE_DOCKER_TAG}" % binary: binary + "-internal",
    },
    stamp = True,
) for binary in DOCKERIZED_BINARIES.keys()]

[container_image(
    name = binary + "-internal",
    base = meta["base"],
    cmd = ["/" + binary],
    files = [meta["target"]],
    symlinks = {
        "/pilot": "/" + binary,
        "/usr/bin/" + binary: "/" + binary,
    },
) for binary, meta in DOCKERIZED_PILOTS.items()]

[container_bundle(
    name = binary,
    images = {
        "jetstackexperimental/navigator-%s:{STABLE_DOCKER_TAG}" % binary: binary + "-internal",
    },
    stamp = True,
) for binary in DOCKERIZED_PILOTS.keys()]

[genrule(
    name = binary + "_docker_tag",
    srcs = [meta["target"]],
    outs = [binary + ".docker_tag"],
    cmd = "grep ^STABLE_DOCKER_TAG bazel-out/stable-status.txt | awk '{print $$2}' >$@",
    stamp = 1,
) for binary, meta in DOCKERIZED_BINARIES.items()]

[genrule(
    name = binary + "_docker_tag",
    srcs = [meta["target"]],
    outs = [binary + ".docker_tag"],
    cmd = "grep ^STABLE_DOCKER_TAG bazel-out/stable-status.txt | awk '{print $$2}' >$@",
    stamp = 1,
) for binary, meta in DOCKERIZED_PILOTS.items()]
